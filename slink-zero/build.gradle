plugins {
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.dokka" version "${dokkaVersion}"
    id "jacoco"
    id "com.github.nbaztec.coveralls-jacoco" version "1.2.15"
}

ext.publishDescription = "Zero dependenices library to boost your Kotlin development"
apply from: '../publish.gradle'

dependencies {
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation "io.kotest:kotest-assertions-core:${kotestVersion}"
    testImplementation "io.kotest:kotest-property:${kotestVersion}"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:${kotestVersion}"
}

kotlin {
    jvmToolchain(11)
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadocJar {
    dependsOn(dokkaHtml)
    archiveClassifier.set("javadoc")
    from(dokkaHtml.outputDirectory)
}

dokkaHtml {
    outputDirectory.set(file("../build/dokka/${project.name}"))
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
    finalizedBy jacocoTestReport
}

signing {
    sign publishing.publications.mavenJava
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
}

tasks.coverallsJacoco.dependsOn(jacocoTestReport)
tasks.publish.dependsOn(test)
